plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'signing'
    id 'tech.yanand.maven-central-publish' version '1.1.1'
}

group = 'tech.yanand.gradle'
version = '1.2.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

java {
    sourceCompatibility = '11'
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    website = 'https://github.com/yananhub/flying-gradle-plugin'
    vcsUrl = 'https://github.com/yananhub/flying-gradle-plugin.git'
    plugins {
        mavenCentralPublishPlugin {
            id = 'tech.yanand.maven-central-publish'
            displayName = 'Maven Central Portal Publish Plugin'
            description = 'A plugin that allows you to publish repositories to the Maven Central Portal (new publish process)'
            tags.addAll('maven', 'maven publish', 'maven central', 'maven central portal')
            implementationClass = 'tech.yanand.gradle.mavenpublish.MavenCentralPublishPlugin'
        }
    }
}

publishing {
    publications.configureEach {
        pom {
            name = 'Maven Central Portal Publish Plugin'
            description = 'A plugin that allows you to publish repositories to the Maven Central Portal (new publish process)'
            url = 'https://github.com/yananhub/flying-gradle-plugin'

            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'yananhub'
                    name = 'Richard Zhang'
                    email = 'yanandwy@163.com'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/yananhub/flying-gradle-plugin.git'
                developerConnection = 'scm:git:https://github.com/yananhub/flying-gradle-plugin.git'
                url = 'https://github.com/yananhub/flying-gradle-plugin'
            }
        }
    }

    repositories {
        maven {
            name = "Local"
            url = layout.buildDirectory.dir('repos/bundles')
        }
    }
}

signing {
    if (System.getenv('SIGNING_KEY')) {
        def signingKey = System.getenv('SIGNING_KEY').replace('\\n', '\n')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
}

javadoc {
    options.addBooleanOption('html5', true)
}

mavenCentral {
    repoDir = layout.buildDirectory.dir('repos/bundles')
    authToken = System.getenv('MAVEN_UPLOAD_TOKEN')
}